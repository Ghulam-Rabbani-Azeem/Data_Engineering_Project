pipeline WorldDevelopmentReportPipeline {

    // Extract Excel data from external link
    block ExcelExtractor oftype HttpExtractor {
        url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }

    // Interpret Excel file, select specific sheet and range
    block ExcelInterpreter oftype XLSXInterpreter {}

    // Block to specify the sheet to extract from the Excel file
    block ExcelSheetPicker oftype SheetPicker {
        sheetName: "Figure S5.1.2";
    }

    // Block to specify the exact range within the selected sheet
    block ExcelRangePicker oftype CellRangeSelector {
        select: range P2:S45;
    }

    // Block to rename header columns explicitly using CellWriter
    block ColumnRenamer oftype CellWriter {
        at: range A1:A4;
        write: [
            "Country Code",           // "ISO3" -> "Country Code"
            "Economy",                // Original column name remains unchanged
            "GDP per Capita",         // "GDP per capita (US$, thousands)" -> "GDP per Capita"
            "Bond Issuance Share"     // "Share of government sustainable bonds" -> "Bond Issuance Share"
        ];
    }

    // Delete unnecessary columns for gdpPerCapita Table
    block GPDTableColumnDeleter oftype ColumnDeleter {
        delete: [column B, column D];
    }

    // Validate the data for GDP table
    block GPDTableDataValidator oftype TableInterpreter {
        header: false;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "GDP per Capita" oftype PositiveDecimal
        ];
    }

    // Delete unnecessary columns for bondIssuance Table
    block BondTableColumnDeleter oftype ColumnDeleter {
        delete: [column B, column C];
    }

    // Validate the data for Bond Issuance table
    block BondTableDataValidator oftype TableInterpreter {
        header: false;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "Bond Issuance Share" oftype ConstrainedDecimal
        ];
    }

    // Load GDP data into SQLite
    block GDPLoader oftype SQLiteLoader {
        table: "gdpPerCapita";
        file: "country-stats.sqlite";
    }

    // Load bond issuance data into SQLite
    block BondIssuanceLoader oftype SQLiteLoader {
        table: "bondIssuance";
        file: "country-stats.sqlite";
    }

    // Constraints and value types for validation
    constraint PositiveDecimalConstraint on decimal:
        value > 0;

    constraint DecimalConstraint on decimal:
        value >= 0 and value <= 1;

    // Define positive decimal type for GDP
    valuetype PositiveDecimal oftype decimal {
        constraints: [PositiveDecimalConstraint];
    }

    // Define constrained decimal type for Bond Issuance Share
    valuetype ConstrainedDecimal oftype decimal {
        constraints: [DecimalConstraint];
    }

    // Define ISO3 country code constraint for validation
    constraint ISOAlpha3CodeConstraint on text:
        value matches /^[A-Za-z]{3}$/;

    // Define ISOAlpha3Code valuetype for country codes
    valuetype CountryCodeAlpha3 oftype text {
        constraints: [ISOAlpha3CodeConstraint];
    }

    // Define the pipeline flow
    ExcelExtractor -> 
    ExcelInterpreter -> 
    ExcelSheetPicker -> 
    ExcelRangePicker -> 
    ColumnRenamer -> 
    GPDTableColumnDeleter -> 
    GPDTableDataValidator -> 
    GDPLoader;

    ColumnRenamer -> 
    BondTableColumnDeleter -> 
    BondTableDataValidator -> 
    BondIssuanceLoader;
}
